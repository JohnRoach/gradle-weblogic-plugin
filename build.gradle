apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: "jacoco"

description 'Gradle plugin for the Weblogic wldeploy ant task'
group 'com.lv.plugins'
version = '0.2'

repositories {
    if( project.hasProperty('internalArtifactRepository') ){
        maven { url project.internalArtifactRepository }
    } else {
        mavenCentral()
        jcenter()
    }
}

dependencies {
    compile localGroovy()
    compile gradleApi()

    testCompile group: 'com.netflix.nebula', name: 'nebula-test', version: '1.12.+'
    testCompile group: 'com.oracle', name: 'wlfullclient', version: '10.3.6'
}

sourceSets {
    integTest {
        groovy.srcDir file('src/integTest/groovy')
        resources.srcDir file('src/integTest/resources')
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }
}

task integTest(type: Test) {
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath
}

//check.dependsOn integTest

/**
 * FOR DEMO PROJECT
 */
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://${project.rootDir}/demo/libs")
        }
    }
}